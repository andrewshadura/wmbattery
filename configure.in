dnl Process this file with autoconf to produce a configure script.
AC_INIT(wmbattery.c)

AC_CONFIG_AUX_DIR(autoconf)
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

dnl Locate X11.
AC_PATH_X
if test ${x_includes:-""} != ""; then
   CPPFLAGS="$CPPFLAGS -I`echo ${x_includes}`"
fi
if test ${x_libraries:-""} != ""; then
   LDFLAGS="$LDFLAGS -L`echo ${x_libraries}`"
fi

dnl Checks for libraries.
AC_CHECK_LIB(X11, XOpenDisplay)
AC_CHECK_LIB(Xext, XShapeCombineMask)
AC_CHECK_LIB(Xpm, XpmReadFileToPixmap)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADER(unistd.h)
AC_CHECK_HEADER(X11/xpm.h)
AC_CHECK_HEADER(X11/extensions/shape.h)
AC_CHECK_HEADER(sys/file.h, CPPFLAGS="$CPPFLAGS -DHAVE_SYS_FILE")
AC_CHECK_HEADER(sys/ioctl.h, CPPFLAGS="$CPPFLAGS -DHAVE_SYS_IOCTL")
AC_CHECK_HEADER(getopt.h, CPPFLAGS="$CPPFLAGS -DHAVE_GETOPT")

dnl FreeBSD needs apm_bios.h
AC_CHECK_HEADER(machine/apm_bios.h, CPPFLAGS="$CPPFLAGS -DHAVE_MACHINE_APM_BIOS")

dnl NetBSD and OpenBSD need apmvar.h (from the kernel headers) for
dnl the definition of apm_power_info.  Look in the likely places.  If
dnl it's not in one of these two places, use CPPFLAGS to pass its
dnl location to configure.
for apmvar_dir in /usr/src/sys/arch/i386/include \
                  /sys/arch/i386/include; do
  AC_CHECK_HEADER(${apmvar_dir}/apmvar.h, CPPFLAGS="$CPPFLAGS -DHAVE_APMVAR -I$apmvar_dir"; break)
done

dnl NetBSD and OpenBSD will compile and run without apmvar.h, but will
dnl fail to display APM status.  Since users are likely to not have
dnl the kernel source installed, and a silent failure will be the
dnl result, check for that error and bail out.
case "${host_os}" in
  ( netbsd* | openbsd* )
    if ! echo "" "$CPPFLAGS" | grep -q "DHAVE_APMVAR"; then
      AC_MSG_ERROR(Can't find apmvar.h--install the system sources or use CPPFLAGS to find them.)
    fi
  ;;
esac

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strdup)

AC_OUTPUT(makeinfo:autoconf/makeinfo.in)
